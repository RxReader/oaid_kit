group 'io.github.v7lin.oaid_kit'
version '0.0.1'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()

        // 私仓
        flatDir {
            dirs project(':oaid_kit').file('libs')
        }
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21

        // library 混淆 -> 随 library 引用，自动添加到 apk 打包混淆
        consumerProguardFiles 'consumer-rules.pro'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    flavorDimensions 'vendor'

    productFlavors {
        vendor {
            dimension 'vendor'

            // library 混淆 -> 随 library 引用，自动添加到 apk 打包混淆
            consumerProguardFiles 'consumer-vendor-rules.pro'

            ndk {
                abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }

    libraryVariants.all { variant ->
        variant.mergeAssetsProvider.get().doLast{ task ->
            println("try to delete 'supplierconfig.json' from library")
            delete(fileTree(task.outputDir) { config ->
                config.include("supplierconfig.json")
            })
        }
    }
}

dependencies {
    //
    vendorImplementation(name: 'oaid_sdk_1.0.25', ext: 'aar')
}
